#!/usr/bin/env perl
## Pombert Lab, Illinois Tech 2023

my $name = 'clusters2links.pl';
my $version = '0.1';
my $updated = '2023-09-26';

use strict;
use warnings;
use Getopt::Long qw(GetOptions);
use File::Basename;
use File::Path qw(make_path);

my $usage = <<"USAGE";
NAME        ${name}
VERSION     ${version}
UPDATED     ${updated}
SYNOPSIS    Parses the output of clusters generated with SYNY to create links for Circos

COMMAND     ${name} \\
              -c *.clusters \\
              -l *.list \\
              -o Circos

OPTIONS:
-c (--cluster)      Cluster file(s) to parse
-l (--list)         Annotations list(s) generated by SYNY list_maker.pl
-o (--outdir)       Output directory [Default: Circos]
USAGE
die "\n$usage\n" unless @ARGV;

my @clusters;
my @lists;
my $outdir = 'Circos';
GetOptions(
    'c|cluster=s@{1,}' => \@clusters,
    'l|list=s@{1,}' => \@lists,
    'o|outdir=s' => \$outdir,
);

### Check if output directory / subdirs can be created
$outdir =~ s/\/$//;
unless (-d $outdir) {
	make_path($outdir,{mode => 0755})  or die "Can't create $outdir: $!\n";
}

my %loci_db;
### Iterating through annotations lists
while (my $list = shift @lists){

    open LIST, '<', $list or die "Can't open $list: $!\n";
    while (my $line = <LIST>){

        chomp $line;

        my @data = split("\t", $line);
        my $locus = $data[0];
        my $contig = $data[1];
        my $start = $data[2] - 1; ## Adjusting position for Circos (starts at zero)
        my $end = $data[3] - 1; ## Same
        my $strand = $data[4];
        my $gene_number = $data[5];

        $loci_db{$locus}{'contig'} = $contig;
        $loci_db{$locus}{'start'} = $start;
        $loci_db{$locus}{'end'} = $end;

    }
}


### Iterating through cluster files

while (my $cluster = shift@clusters){

    my $filename = fileparse($cluster);
    my ($prefix) = $filename =~ /^(\w+)/;
    my $outlinks = $outdir.'/'.$prefix.'.links';

    open CLUSTER, "<", $cluster or die "Can't open $cluster: $!\n";
    open OUT, ">", $outlinks or die "Can't create $outlinks\n";
    print OUT "#locus1 start end locus2 start end\n";

    my %queries;

    while (my $line = <CLUSTER>){

        chomp $line;

        if ($line =~ /^### Cluster (\d+)/){

            my @data = split ("; ", $line);
            my $cluster_num = $data[0];
            my $query = $data[1];
            my $subject = $data[2];
            my $size = $data[3];

            my ($qstart,$qend) = $query =~ /^(\w+) to (\w+)/;
            my ($sstart,$send) = $subject =~ /^(\w+) to (\w+)/;

            # query
            print OUT $loci_db{$qstart}{'contig'}.' ';
            print OUT $loci_db{$qstart}{'start'}.' ';
            print OUT $loci_db{$qend}{'end'}.' ';
            # subject
            print OUT $loci_db{$sstart}{'contig'}.' ';
            print OUT $loci_db{$sstart}{'start'}.' ';
            print OUT $loci_db{$send}{'end'}."\n";

        }

    }

}